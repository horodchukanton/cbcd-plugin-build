plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'maven-publish'
}

group 'com.cloudbees.cd.plugins'
version '1.6.8' + (Boolean.valueOf(findProperty("release")) ? ".0" : "-SNAPSHOT")
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
    // Plugin should be declared as a dependency, so we can inject it to another projects
    implementation "io.qameta.allure:allure-gradle:2.8.1"
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}


pluginBundle {
    website = 'https://com.cloudbees.com/'
    vcsUrl = 'https://github.com/horodchukanton/cbcd-plugin-build'
    tags = ['specs', 'CloudBees', 'internal', 'plugins']
}

gradlePlugin {
    plugins {
        specsPlugin {
            id = "com.cloudbees.cd.plugins.specs"
            displayName = "cbcd-plugins-specs"
            description = "Collection of Gradle tasks and dependencies for CloudBees CD plugin specs projects"
            implementationClass = "com.cloudbees.cd.plugins.build.CDPluginBuild"
        }
    }
}